apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "com.fada21.android.politikon"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled false
            useProguard false
            // run coverage with ./gradlew -Pcoverage ...
            testCoverageEnabled(project.hasProperty('coverage'))
        }

        release {
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
        }
    }

    productFlavors {
        mock {
            applicationIdSuffix = ".mock"
        }
        dev {
            applicationIdSuffix = ".dev"
            minSdkVersion 21
        }
        prod
    }

    // Remove non prodRelease as it's not needed.
    android.variantFilter { variant ->
        if (variant.buildType.name.equals('release')
                && !variant.getFlavors()[0].name.equals('prod')) {
            variant.setIgnore(true);
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

ext {
    support_version = '25.0.1'
    rx_version = '1.2.1'
    retrofit_version = '2.1.0'
    butter_knife = '8.2.1'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile "com.android.support:appcompat-v7:${support_version}"
    compile "com.android.support:design:${support_version}"
    compile "com.android.support:recyclerview-v7:${support_version}"
    compile "com.android.support:cardview-v7:${support_version}"

    compile "io.reactivex:rxandroid:${rx_version}"
    compile "io.reactivex:rxjava:${rx_version}"

    compile "com.squareup.retrofit2:retrofit:${retrofit_version}"
    compile "com.squareup.retrofit2:converter-moshi:${retrofit_version}"
    compile "com.squareup.retrofit2:adapter-rxjava:${retrofit_version}"

    compile 'com.squareup.picasso:picasso:2.5.2'

    compile "com.jakewharton:butterknife:${butter_knife}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${butter_knife}"

    compile 'net.danlew:android.joda:2.9.4.1'
}
